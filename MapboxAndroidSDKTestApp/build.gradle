buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs '../libs'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        applicationId "com.mapbox.mapboxsdk.android.testapp"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // This is important, it will run lint checks but won't abort build
//    lintOptions {
//        abortOnError false
//    }
}

dependencies {
    implementation(project(':MapboxAndroidSDK')) {
        transitive = true
    }
    implementation 'com.android.support:support-annotations:22.2.0'
    implementation 'com.android.support:appcompat-v7:22.2.1'
    implementation 'com.android.support:support-v4:22.2.1'
    implementation 'com.android.support:design:22.2.1'
}


checkstyle {
    configFile project.file('../checks.xml')
    showViolations true
}

android.applicationVariants.all { variant ->
    def name = variant.buildType.name
    def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
    checkstyle.dependsOn variant.javaCompile
    checkstyle.source variant.javaCompile.source
    checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
    checkstyle.exclude('**/BuildConfig.java')
    checkstyle.exclude('**/R.java')
    project.tasks.getByName("check").dependsOn checkstyle
}
